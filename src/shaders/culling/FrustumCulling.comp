//
layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

struct AABB {
  float pt[6];
};

struct DrawIndexedIndirectCommand {
  uint count;
  uint instanceCount;
  uint firstIndex;
  int  baseVertex;
  uint baseInstance;
};

struct DrawData {
  uint transformId;
  uint materialId;
};

layout(std430, buffer_reference) readonly buffer BoundingBoxes {
  AABB boxes[];
};

layout(std430, buffer_reference) readonly buffer DrawDataBuffer {
  DrawData dd[];
};

layout(std430, buffer_reference) buffer DrawCommands {
  uint dummy;
  DrawIndexedIndirectCommand dc[];
};

layout(std430, buffer_reference) buffer CullingData {
  vec4 planes[6];
  vec4 corners[8];
  uint numMeshesToCull;
  uint numVisibleMeshes;
};

layout(std430, push_constant) uniform PushConstants {
  DrawCommands commands;
  DrawDataBuffer drawData;
  BoundingBoxes AABBs;
  CullingData frustum;
};

#define Box_min_x box.pt[0]
#define Box_min_y box.pt[1]
#define Box_min_z box.pt[2]
#define Box_max_x box.pt[3]
#define Box_max_y box.pt[4]
#define Box_max_z box.pt[5]

// this is slow - use at your own risk (and just for educational purposes)!
bool isAABBinFrustum(AABB box)
{
  if (
    dot( frustum.planes[0], vec4(Box_min_x, Box_min_y, Box_min_z, 1.0f) ) < 0 && 
    dot( frustum.planes[0], vec4(Box_max_x, Box_min_y, Box_min_z, 1.0f) ) < 0 && 
    dot( frustum.planes[0], vec4(Box_min_x, Box_max_y, Box_min_z, 1.0f) ) < 0 &&
    dot( frustum.planes[0], vec4(Box_max_x, Box_max_y, Box_min_z, 1.0f) ) < 0 &&
    dot( frustum.planes[0], vec4(Box_min_x, Box_min_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[0], vec4(Box_max_x, Box_min_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[0], vec4(Box_min_x, Box_max_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[0], vec4(Box_max_x, Box_max_y, Box_max_z, 1.0f) ) < 0
  ) {
    return false;
  }

  if (
    dot( frustum.planes[1], vec4(Box_min_x, Box_min_y, Box_min_z, 1.0f) ) < 0 && 
    dot( frustum.planes[1], vec4(Box_max_x, Box_min_y, Box_min_z, 1.0f) ) < 0 && 
    dot( frustum.planes[1], vec4(Box_min_x, Box_max_y, Box_min_z, 1.0f) ) < 0 &&
    dot( frustum.planes[1], vec4(Box_max_x, Box_max_y, Box_min_z, 1.0f) ) < 0 &&
    dot( frustum.planes[1], vec4(Box_min_x, Box_min_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[1], vec4(Box_max_x, Box_min_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[1], vec4(Box_min_x, Box_max_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[1], vec4(Box_max_x, Box_max_y, Box_max_z, 1.0f) ) < 0
  ) {
    return false;
  }

  if (
    dot( frustum.planes[2], vec4(Box_min_x, Box_min_y, Box_min_z, 1.0f) ) < 0 && 
    dot( frustum.planes[2], vec4(Box_max_x, Box_min_y, Box_min_z, 1.0f) ) < 0 && 
    dot( frustum.planes[2], vec4(Box_min_x, Box_max_y, Box_min_z, 1.0f) ) < 0 &&
    dot( frustum.planes[2], vec4(Box_max_x, Box_max_y, Box_min_z, 1.0f) ) < 0 &&
    dot( frustum.planes[2], vec4(Box_min_x, Box_min_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[2], vec4(Box_max_x, Box_min_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[2], vec4(Box_min_x, Box_max_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[2], vec4(Box_max_x, Box_max_y, Box_max_z, 1.0f) ) < 0
  ) {
    return false;
  }

  if (
    dot( frustum.planes[3], vec4(Box_min_x, Box_min_y, Box_min_z, 1.0f) ) < 0 && 
    dot( frustum.planes[3], vec4(Box_max_x, Box_min_y, Box_min_z, 1.0f) ) < 0 && 
    dot( frustum.planes[3], vec4(Box_min_x, Box_max_y, Box_min_z, 1.0f) ) < 0 &&
    dot( frustum.planes[3], vec4(Box_max_x, Box_max_y, Box_min_z, 1.0f) ) < 0 &&
    dot( frustum.planes[3], vec4(Box_min_x, Box_min_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[3], vec4(Box_max_x, Box_min_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[3], vec4(Box_min_x, Box_max_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[3], vec4(Box_max_x, Box_max_y, Box_max_z, 1.0f) ) < 0
  ) {
    return false;
  }

  if (
    dot( frustum.planes[4], vec4(Box_min_x, Box_min_y, Box_min_z, 1.0f) ) < 0 && 
    dot( frustum.planes[4], vec4(Box_max_x, Box_min_y, Box_min_z, 1.0f) ) < 0 && 
    dot( frustum.planes[4], vec4(Box_min_x, Box_max_y, Box_min_z, 1.0f) ) < 0 &&
    dot( frustum.planes[4], vec4(Box_max_x, Box_max_y, Box_min_z, 1.0f) ) < 0 &&
    dot( frustum.planes[4], vec4(Box_min_x, Box_min_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[4], vec4(Box_max_x, Box_min_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[4], vec4(Box_min_x, Box_max_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[4], vec4(Box_max_x, Box_max_y, Box_max_z, 1.0f) ) < 0
  ) {
    return false;
  }

  if (
    dot( frustum.planes[5], vec4(Box_min_x, Box_min_y, Box_min_z, 1.0f) ) < 0 && 
    dot( frustum.planes[5], vec4(Box_max_x, Box_min_y, Box_min_z, 1.0f) ) < 0 && 
    dot( frustum.planes[5], vec4(Box_min_x, Box_max_y, Box_min_z, 1.0f) ) < 0 &&
    dot( frustum.planes[5], vec4(Box_max_x, Box_max_y, Box_min_z, 1.0f) ) < 0 &&
    dot( frustum.planes[5], vec4(Box_min_x, Box_min_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[5], vec4(Box_max_x, Box_min_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[5], vec4(Box_min_x, Box_max_y, Box_max_z, 1.0f) ) < 0 &&
    dot( frustum.planes[5], vec4(Box_max_x, Box_max_y, Box_max_z, 1.0f) ) < 0
  ) {
    return false;
  }

  if (
    frustum.corners[0].x > Box_max_x && 
    frustum.corners[1].x > Box_max_x && 
    frustum.corners[2].x > Box_max_x &&
    frustum.corners[3].x > Box_max_x &&
    frustum.corners[4].x > Box_max_x &&
    frustum.corners[5].x > Box_max_x &&
    frustum.corners[6].x > Box_max_x &&
    frustum.corners[7].x > Box_max_x
  ) {
    return false;
  }

  if (
    frustum.corners[0].x < Box_min_x && 
    frustum.corners[1].x < Box_min_x && 
    frustum.corners[2].x < Box_min_x &&
    frustum.corners[3].x < Box_min_x &&
    frustum.corners[4].x < Box_min_x &&
    frustum.corners[5].x < Box_min_x &&
    frustum.corners[6].x < Box_min_x &&
    frustum.corners[7].x < Box_min_x
  ) {
    return false;
  }

  if (
    frustum.corners[0].y > Box_max_y && 
    frustum.corners[1].y > Box_max_y && 
    frustum.corners[2].y > Box_max_y &&
    frustum.corners[3].y > Box_max_y &&
    frustum.corners[4].y > Box_max_y &&
    frustum.corners[5].y > Box_max_y &&
    frustum.corners[6].y > Box_max_y &&
    frustum.corners[7].y > Box_max_y
  ) {
    return false;
  }

  if (
    frustum.corners[0].y < Box_min_y && 
    frustum.corners[1].y < Box_min_y && 
    frustum.corners[2].y < Box_min_y &&
    frustum.corners[3].y < Box_min_y &&
    frustum.corners[4].y < Box_min_y &&
    frustum.corners[5].y < Box_min_y &&
    frustum.corners[6].y < Box_min_y &&
    frustum.corners[7].y < Box_min_y
  ) {
    return false;
  }

  if (
    frustum.corners[0].z > Box_max_z && 
    frustum.corners[1].z > Box_max_z && 
    frustum.corners[2].z > Box_max_z &&
    frustum.corners[3].z > Box_max_z &&
    frustum.corners[4].z > Box_max_z &&
    frustum.corners[5].z > Box_max_z &&
    frustum.corners[6].z > Box_max_z &&
    frustum.corners[7].z > Box_max_z
  ) {
    return false;
  }

  if (
    frustum.corners[0].z < Box_min_z && 
    frustum.corners[1].z < Box_min_z && 
    frustum.corners[2].z < Box_min_z &&
    frustum.corners[3].z < Box_min_z &&
    frustum.corners[4].z < Box_min_z &&
    frustum.corners[5].z < Box_min_z &&
    frustum.corners[6].z < Box_min_z &&
    frustum.corners[7].z < Box_min_z
  ) {
    return false;
  }

  return true;
}

void main()
{
  const uint idx = gl_GlobalInvocationID.x;

  // skip items beyond scene.meshForNode.size()
  if (idx < frustum.numMeshesToCull) {
    uint baseInstance = commands.dc[idx].baseInstance;
    AABB box = AABBs.boxes[drawData.dd[baseInstance].transformId];
    uint numInstances = isAABBinFrustum(box) ? 1 : 0;
    commands.dc[idx].instanceCount = numInstances;
  }
}